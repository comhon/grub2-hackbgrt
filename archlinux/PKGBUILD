# vim: set et sw=2 sts=2 ts=2:
# Maintainer: Cyrille Pontvieux <cyrille [at] enialis [dot] net>

# "1" to enable IA32-EFI build in Arch x86_64, "0" to disable
_ia32_efi_in_arch_x64="0"
_grubver="2.04"

[ "${CARCH}" = "x86_64" ] && _target_arch="x86_64"
[ "${CARCH}" == "i686" ] && _target_arch="i386"
_build_platforms="${_target_arch}"
[ "${CARCH}" = "x86_64" ] && [ "${_ia32_efi_in_arch_x64}" = "1" ] && _build_platforms+=" i386"

pkgname=grub2-hackbgrt
pkgver=0.3.0
pkgrel=1
pkgdesc="Hack BGRT for Grub2 (EFI)"
arch=('x86_64' 'i686')
url="https://github.com/jrd/${pkgname}"
license=('GPL3')
depends=('grub')
makedepends=(
  'autogen'
  'gettext'
  'git'
  'help2man'
  'python'
  'texinfo'
)
options=('!buildflags')
install=${pkgname}.install
backup=(etc/grub.d/01_hackbgrt)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jrd/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  grubver=${_grubver} make prepare
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  for _platform in $_build_platforms; do
    grubver=${_grubver} platform=${_platform} make compile
  done
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  for _platform in $_build_platforms; do
    grubver=${_grubver} platform=${_platform} DESTDIR=${pkgdir} make install-module install-lst install-grub-hackbgrt-conf
  done
}
